90 REM FROM @MARKTHESTRANGE
100 DIM BT$(155)
110 REM FIND START OF KEYWORD TABLE
120 BANK 0, 4
130 KT = $C100
140 IF PEEK(KT)   <> $45 THEN KT=KT+1:GOTO 140
150 IF PEEK(KT+1) <> $4E THEN KT=KT+1:GOTO 140
160 IF PEEK(KT+2) <> $C4 THEN KT=KT+1:GOTO 140
170 PRINT "KEYWORD TABLE STARTS AT $" HEX$(KT) "."
180 PRINT "LOADING KEYWORDS";
190 FOR S=0 TO 2
200 : FOR C = PEEK(KT) TO 0 STEP 0
210 :   KT = KT +1
220 :   BT$(T) = BT$(T)+CHR$(C AND 127)
230 :   IF C AND 128 THEN T = T + 1
240 :   C = PEEK(KT)
250 : NEXT C
260 : PRINT " .";
270 NEXT S
275 PRINT " DONE."
280 FOR I=0 TO T - 1 STEP 3
290 : FOR J=0 TO 2
300 :   K=I+J:IF K>=T THEN 340
310 :   IF K>75 THEN PRINT "206,"K-76+128,
320 :   IF K<=75 THEN PRINT K+128,
330 :   PRINT BT$(K),
340 : NEXT J
350 : PRINT
360 NEXT
